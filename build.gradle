buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: "forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

minecraft {
    mappings = 'stable_12'
	version = mc_version + "-" + forge_version
	runDir = "run"
	
	srgExtra "PK: org/apache/commons/lang3 com/hellzing/shaded/apache/commons/lang3"
	srgExtra "PK: org/apache/commons/collections4 com/hellzing/shaded/apache/commons/collections4"
	srgExtra "PK: org/json com/hellzing/shaded/json"
	srgExtra "PK: com/sun/jna com/hellzing/shaded/jna"
	srgExtra "PK: org/json com/hellzing/shaded/json"
	srgExtra "PK: com/neovisionaries/ws/client com/hellzing/shaded/ws/client"
	srgExtra "PK: com/mashape/unirest com/hellzing/shaded/unirest"
	srgExtra "PK: com/vdurmont/emoji com/hellzing/shaded/emoji"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
	maven {
		name = "dv8fromtheworld"
		url = "https://dl.bintray.com/dv8fromtheworld/maven"
	}
	maven {
		name = "jitpack.io"
		url = "https://jitpack.io"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	compileOnly "org.projectlombok:lombok:1.16.10"
	shade group: "net.dv8tion", name: "JDA", version: "3.0.0_165"
	shade group: "com.github.MrPowerGamerBR", name: "TemmieWebhook", version: "-SNAPSHOT"
	shade group:  "com.vdurmont", name: "emoji-java", version: "3.1.3"
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude "META-INF", "META-INF/**"
		}
	}
}
